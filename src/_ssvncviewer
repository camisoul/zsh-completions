#compdef ssvncviewer
#
# --------------------------------------------------------------------------
#
# Description :
#   @(#) zsh completion script for ssvncviewer
#
# --------------------------------------------------------------------------
# Copyright (c) 2015 camisoul<camisoulmax@gmail.com>
#
# This software is provided 'as-is', without any express or implied
# warranty.  In no event will the authors be held liable for any damages
# arising from the use of this software.
#
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it
# freely, subject to the following restrictions:
#
# 1. The origin of this software must not be misrepresented; you must not
#    claim that you wrote the original software. If you use this software
#    in a product, an acknowledgment in the product documentation would be
#    appreciated but is not required.
# 2. Altered source versions must be plainly marked as such, and must not be
#    misrepresented as being the original software.
# 3. This notice may not be removed or altered from any source distribution.
# --------------------------------------------------------------------------


local ret=1
_arguments \
  '(- : *)-help[display help and exit]' \
  '(- : *)-version[output version information and exit]' \
  '-listen[Make the viewer listen on port 5500+display for reverse connections from a server]' \
  '-via[Automatically create encrypted TCP tunnel to the gateway machine before connection]:gateway' \
  '-shared[When connecting, specify that a shared connection is requested]' \
  '-noshared[When connecting, specify that the session may not be shared]' \
  '-viewonly[Disable transfer of mouse and keyboard events from the client to the server]' \
  '-fullscreen[Start in full-screen mode]' \
  '-noraiseonbeep[By default, the viewer shows and raises its window on remote beep (bell) event]' \
  '-user[User name for Unix login authentication]:username:_users' \
  '-passwd[File from which to get the password]:passwd-file:_files' \
  '-encodings[supports several different compression methods to encode screen updates]:encodings:(copyrect tight hextile zlib corre rre raw)' \
  '-bgr233[Always use the BGR233 format to encode pixel data]' \
  '-owncmap[Try to use a PseudoColor visual and a private colormap]' \
  '(-truecolour -truecolor)'{-truecolour,-truecolor}'[Try to use a TrueColor visual]' \
  '-owncmap[Try to use a PseudoColor visual and a private colormap]' \
  '-depth[an X server which supports multiple TrueColor visuals of different depths, attempt to use the specified one]:depth' \
  '(-nojpeg)-compresslevel[level Use specified compression level (0..9) for "tight" and "zlib" encodings]:level' \
  '(-nojpeg)-quality[Use the specified JPEG quality level (0..9) for the "tight" encoding]:level' \
  '(-quality -compresslevel)-nojpeg[Disable lossy JPEG compression in Tight encoding]' \
  '-nocursorshape[Disable cursor shape updates]' \
  '-x11cursor[Use a real X11 cursor with X-style cursor shape updates]' \
  '-autopass[Read a plain-text password from stdin]' \
  '-multilisten[As in -listen except allow more than one incoming VNC server to be connected at a time]' \
  '-acceptpopup[In -listen mode when a reverse VNC connection comes in show a popup asking whether to Accept or Reject the connection]' \
  '-acceptpopupsc[As in -acceptpopup except assume UltraVNC Single Click (SC) server]' \
  '(-use64 -bgr222)'{-use64,-bgr222}'[In -bgr233 mode, use 64 colors instead of 256]' \
  '(-use8 -bgr111)'{-use8,-bgr111}'[In -bgr233 mode, use 8 colors instead of 256]' \
  '(-bgr565 -16bpp)'{-16bpp,-bgr565}'[If the vnc viewer X display is depth 24 at 32bpp request a 16bpp format from the VNC server]' \
  '-grey[Use a grey scale for the 16- and 8-bpp modes]' \
  '-alpha[Use alphablending transparency for local cursors requires]' \
  '-scale[Scale the desktop locally]:scale' \
  '-ycrop[Only show the top n rows of the framebuffer]:n' \
  '-sbwidth[Scrollbar width for x11vnc -ncache mode (-ycrop), default is very narrow]:n' \
  '-nobell[Disable bell]' \
  '-rawlocal[Prefer raw encoding for localhost]' \
  '-notty[Try to avoid using the terminal for interactive responses]' \
  '-sendclipboard[Send the X CLIPBOARD selection]' \
  '-sendalways[Whenever the mouse enters the VNC viewer main window, send the selection to the VNC server even if it has not changed]' \
  '-recvtext[Ssvncviewer will set both the X PRIMARY and the X CLIPBOARD local selections]:str' \
  '-graball[Grab the entire X server when in fullscreen mode, needed by some old window managers like fvwm2]' \
  '-popupfix[Warp the popup back to the pointer position, needed by some old window managers like fvwm2]' \
  '-grabkbd[Grab the X keyboard when in fullscreen mode, needed by some window managers]' \
  '(-bs -nobs)'{-bs,-nobs}'[Whether or not to use X server Backingstore for the main viewer window]' \
  '-noshm[Disable use of MIT shared memory extension (not recommended)]' \
  '-termchat[Do the UltraVNC chat in the terminal vncviewer is in instead of in an independent window]' \
  '-unixpw[Useful for logging into x11vnc in -unixpw mode]:str' \
  '-repeater[This is for use with UltraVNC repeater proxy described here]:str' \
  '-rfbversion[Set the advertised RFB version]:str' \
  '-ultradsm[UltraVNC has symmetric private encryption DSM plugins]' \
  '-mslogon[Use Windows MS Logon to an UltraVNC server]:user' \
  '-chatonly[Try to be a client that only does UltraVNC text chat]' \
  '-env[To save writing a shell script to set environment variables, specify as many as you need on the command line]:VAR=VALUE' \
  '-noipv6[Disable all IPv6 sockets]' \
  '-noipv4[Disable all IPv4 sockets]' \
  '-printres[Print out the Ssvnc X resources and then exit]' \
  '-pipeline[Like TurboVNC, request the next framebuffer update as soon as possible]' \
  "-appshare[Enable features for use with x11vnc's -appshare mode]" \
  "-escape[This sets the 'Escape Keys' modifier sequence and enables escape keys mode]:str:(r f l s t b x g + c z e - a V Q 1 2 3 4 5 6)" \
  '(- : *)-h[display help and exit]' \
  '::file:_hosts' && ret=0

return ret
