#compdef nemo
#
# --------------------------------------------------------------------------
#
# Description :
#   @(#) zsh completion script for nemo
#
# --------------------------------------------------------------------------
# Copyright (c) 2015 camisoul<camisoulmax@gmail.com>
#
# This software is provided 'as-is', without any express or implied
# warranty.  In no event will the authors be held liable for any damages
# arising from the use of this software.
#
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it
# freely, subject to the following restrictions:
#
# 1. The origin of this software must not be misrepresented; you must not
#    claim that you wrote the original software. If you use this software
#    in a product, an acknowledgment in the product documentation would be
#    appreciated but is not required.
# 2. Altered source versions must be plainly marked as such, and must not be
#    misrepresented as being the original software.
# 3. This notice may not be removed or altered from any source distribution.
# --------------------------------------------------------------------------

local ret=1

_arguments -s -S \
  '(- : *)--help[display help and exit]' \
  '(- : *)--version[output version information and exit]' \
  '(-c --check)'{-c,--check}'[perform a quick set of self-check tests]' \
  '(-g --geometry)'{-g+,--geometry=}'[create the initial window with the given geometry]:GEOMETRY:_x_geometry' \
  '(-n --no-default-window)'{-n,--no-default-window}'[only create windows for explicitly specified URIs]' \
  '(- : *)'{-q,--quit}'[quit nemo]' \
  '--no-desktop[do not manage the desktop]' \
  '--display=[X display to use]:display:_x_display' \
  ':URIs:->uris' && ret=0

case $state in
  uris)
    _alternative \
      'directory::_files' \
      'url::_urls'
    ;;
esac

return ret
