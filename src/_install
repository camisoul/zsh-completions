#compdef install
#
# --------------------------------------------------------------------------
#
# Description :
#   @(#) zsh completion script for install
#
# --------------------------------------------------------------------------
# Copyright (c) 2015 camisoul<camisoulmax@gmail.com>
#
# This software is provided 'as-is', without any express or implied
# warranty.  In no event will the authors be held liable for any damages
# arising from the use of this software.
#
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it
# freely, subject to the following restrictions:
#
# 1. The origin of this software must not be misrepresented; you must not
#    claim that you wrote the original software. If you use this software
#    in a product, an acknowledgment in the product documentation would be
#    appreciated but is not required.
# 2. Altered source versions must be plainly marked as such, and must not be
#    misrepresented as being the original software.
# 3. This notice may not be removed or altered from any source distribution.
# --------------------------------------------------------------------------

local ret=1

LC_MESSAGES=C _arguments -- \
  '*=DEST*:DEST:_directories' \
  '*=GROUP*:GROUP:_groups' \
  '*=OWNER*:OWNER:_users' \
  '*=MODE*:MODE:->mode' && ret=0

if [[ ${state} == mode ]]; then
  compset -P \*,
  compset -S ,\*
  if [[ -prefix [0-7] ]]; then
    _message -e number 'numeric mode'
  elif compset -P '[a-z]#[+-=]'; then
    _values -S '' privilege \
      'r[read]' 'w[write]' 'x[execute]' \
      's[set uid/gid]' 't[sticky]' \
      'X[execute only if directory or executable to another]' \
      "u[owner's current permissions]" \
      "g[group's current permissions]" \
      "o[other's current permissions]" \
      "${privs[@]}" && ret=0
  else
    suf=( -S '' )
    compset -P '*'
    _alternative -O suf \
      'who:who:((u\:user g\:group a\:all o\:others))' \
      'operators:operator:(+ - =)' && ret=0
  fi
fi

return ret
