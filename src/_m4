#compdef m4
#
# --------------------------------------------------------------------------
#
# Description :
#   @(#) zsh completion script for m4
#
# --------------------------------------------------------------------------
# Copyright (c) 2015 camisoul<camisoulmax@gmail.com>
#
# This software is provided 'as-is', without any express or implied
# warranty.  In no event will the authors be held liable for any damages
# arising from the use of this software.
#
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it
# freely, subject to the following restrictions:
#
# 1. The origin of this software must not be misrepresented; you must not
#    claim that you wrote the original software. If you use this software
#    in a product, an acknowledgment in the product documentation would be
#    appreciated but is not required.
# 2. Altered source versions must be plainly marked as such, and must not be
#    misrepresented as being the original software.
# 3. This notice may not be removed or altered from any source distribution.
# --------------------------------------------------------------------------

local ret=1
_arguments -s -S \
  '(- : *)--help[display help and exit]' \
  '(- : *)--version[output version information and exit]' \
  {-E,--fatal-warnings}'[once warnings become errors, twice stop execution at first error]' \
  '(-i --interactive)'{-i,--interactive}'[unbuffer output, ignore interrupts]' \
  "(-P --prefix-builtins)"{-P,--prefix-builtins}"[force a \`m4_' prefix to all builtins]" \
  '(-Q --quiet --silent)'{-Q,--quiet,--silent}'[suppress some warnings for builtins]' \
  '--warn-macro-sequence=-[warn if macro definition matches REGEXP]:REGEXP' \
  '*'{-D+,--define=}'[define NAME as having VALUE, or empty]:NAME(=VALUE)' \
  '(-I --include)'{-I+,--include=}'[append DIRECTORY to include path]:DIRECTORY:_files -/' \
  "(-s --synclines)"{-s+,--synclines}"[generate \`#line NUM \"FILE\"' lines]" \
  '(-U --undefine)'{-U+,--undefine=}'[undefine NAME]:NAME' \
  '(-g --gnu)'{-g,--gnu}'[override -G to re-enable GNU extensions]' \
  '(-G --traditional)'{-G,--traditional}'[suppress all GNU extensions]' \
  '(-H --hashsize)'{-H+,--hashsize=}'[set symbol lookup hash table size (509)]:PRIME' \
  '(-L --nesting-limit)'{-L+,--nesting-limit=}'[change nesting limit, 0 for unlimited (0)]:NUMBER' \
  '(-F --freeze-state)'{-F+,--freeze-state=}'[produce a frozen state on FILE at end]:FILE:_files' \
  '(-R --reload-state)'{-R+,--reload-state=}'[reload a frozen state from FILE at start]:FILE:_files' \
  '(-d --debug)'{-d-,--debug=-}"[set debug level (no FLAGS implies \`aeq')]:FLAGS:->flags" \
  '--debugfile=-[redirect debug and trace output to FILE]:file:_files' \
  '(-l --arglength)'{-l+,--arglength=}'[restrict macro tracing size]:NUM' \
  '(-t --trace)'{-t+,--trace=}'[trace NAME when it is defined]:NAME' \
  '*:file:_files' && ret=0

if [[ ${state} == flags ]]; then
  local OLDIFS=${IFS}
  IFS=$'\n'
  _values -s '' 'flags' $(m4 --help | sed -nr '/^FLAGS/!d;:a;n;s/^\s*([^ ])\s*(.*)$/\1[\2]/p;ta;q') && ret=0
  IFS=${OLDIFS}
fi

return ret
