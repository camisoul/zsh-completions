#compdef lsdiff filterdiff grepdiff
#
# --------------------------------------------------------------------------
#
# Description :
#   @(#) zsh completion script for lsdiff filterdiff grepdiff
#
# --------------------------------------------------------------------------
# Copyright (c) 2015 camisoul<camisoulmax@gmail.com>
#
# This software is provided 'as-is', without any express or implied
# warranty.  In no event will the authors be held liable for any damages
# arising from the use of this software.
#
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it
# freely, subject to the following restrictions:
#
# 1. The origin of this software must not be misrepresented; you must not
#    claim that you wrote the original software. If you use this software
#    in a product, an acknowledgment in the product documentation would be
#    appreciated but is not required.
# 2. Altered source versions must be plainly marked as such, and must not be
#    misrepresented as being the original software.
# 3. This notice may not be removed or altered from any source distribution.
# --------------------------------------------------------------------------

local ret=1
local -a common_args filter_args filename_args
common_args=(
  '(-x --exclude -X --exclude-from-file)'{-x,--exclude=}'[exclude files matching PAT]:PAT'
  '(-x --exclude -X --exclude-from-file)'{-X,--exclude-from-file=}'[exclude files that match any pattern in FILE]:FILE:_files'
  '(-i --include -I --include-from-file)'{-i,--include=}'[include only files matching PAT]:PAT'
  '(-i --include -I --include-from-file)'{-I,--include-from-file=}'[include only files that match any pattern in FILE]:FILE:_files'
  '(--hunks -\#)'{--hunks=,-\#}'[include only hunks in range H]:H'
  '--lines=[include only hunks with (original) lines in range L]:L'
  '--files=[include only files in range F]:F'
  '(-z --decompress)'{-z,--decompress}'[decompress .gz and .bz2 files]'
  '(-n --line-number)'{-n,--line-number}'[show line numbers]'
  '(-p --strip-match)'{-p,--strip-match=}'[initial pathname components to ignore]:N'
  "--number-files[show file numbers, for use with filterdiff's --files option]"
  '--strip=[initial pathname components to strip]:N'
  '--addprefix=[prefix pathnames with PREFIX]:PREFIX'
  '--addoldprefix=[prefix pathnames in old files with PREFIX]:PREFIX'
  '--addnewprefix=[prefix pathnames in new files with PREFIX]:PREFIX'
  '(-s --status)'{-s,--status}'[show file additions and removals]'
  '(-v --verbose)*'{-v,--verbose}'[verbose output -- use more than once for extra verbosity]'
  '(-E --extended-regexp --empty-files-as-absent)'{-E,--extended-regexp,--empty-files-as-absent}'[treat empty files as absent]'
  '(-f --file)'{-f,--file=}':FILE:_files'
  '(- : *)--help[display help and exit]'
  '(- : *)--version[output version information and exit]'
  '*:file:_files'
)

filename_args=(
  '(-H --with-filename -h --no-filename)'{-H,--with-filename}'[show patch file names]'
  '(-H --with-filename -h --no-filename)'{-h,--no-filename}'[suppress patch file names]'
)

filter_args=(
  '--annotate[annotate each hunk with the filename and hunk number]'
  '--as-numbered-lines=[display lines as they would look before, or after]:lines:(before after)'
  '--format=[set output format]:format:(context unified)'
  "--remove-timestamps[don't show timestamps from output]"
)

case "${service}" in
  lsdiff)
    _arguments -s -S \
      "--number-files[show file numbers, for use with filterdiff's --files option]" \
      '--filter[run as filterdiff]' \
      '--grep[run as grepdiff]' \
      "${filename_args[@]}" \
      "${common_args[@]}" && ret=0
    ;;
  filterdiff)
    _arguments -s -S \
      '--clean[remove all comments (non-diff lines) from output]' \
      '--list[run as lsdiff]' \
      '--grep[run as grepdiff]' \
      "${filter_args[@]}" \
      "${common_args[@]}" && ret=0
    ;;
  grepdiff)
    _arguments -s -S -A "-*" \
      '--output-matching=[show matching hunks or file-level diffs]:show:(hunk file)' \
      '(-):REGEX' \
      '--filter[run as filterdiff]' \
      '--list[run as lsdiff]' \
      "${filename_args[@]}" \
      "${filter_args[@]}" \
      "${common_args[@]}" && ret=0
    ;;
esac

return ret
