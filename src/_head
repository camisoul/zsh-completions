#compdef head tail
#
# --------------------------------------------------------------------------
#
# Description :
#   @(#) zsh completion script for head tail
#
# --------------------------------------------------------------------------
# Copyright (c) 2015 camisoul<camisoulmax@gmail.com>
#
# This software is provided 'as-is', without any express or implied
# warranty.  In no event will the authors be held liable for any damages
# arising from the use of this software.
#
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it
# freely, subject to the following restrictions:
#
# 1. The origin of this software must not be misrepresented; you must not
#    claim that you wrote the original software. If you use this software
#    in a product, an acknowledgment in the product documentation would be
#    appreciated but is not required.
# 2. Altered source versions must be plainly marked as such, and must not be
#    misrepresented as being the original software.
# 3. This notice may not be removed or altered from any source distribution.
# --------------------------------------------------------------------------

local ret=1
local -a args
args=(
  '(- : *)--help[display help and exit]'
  '(- : *)--version[output version information and exit]'
  '(-q --quiet --silent -v --verbose)'{-v,--verbose}'[always print headers giving file names]'
  '(-q --quiet --silent -v --verbose)'{-q,--quiet,--silent}'[never print headers giving file names]'
  '*:FILE:_files'
)
if [[ ${service} == head ]]; then
  _arguments -s -S ${args[@]} \
    '(-c --bytes -n --lines)'{-c+,--bytes=}'[print the first K bytes of each file]: :->k_balue' \
    '(-c --bytes -n --lines)'{-n+,--lines=}'[print the first K lines instead of the first 10]: :->k_value' \
    && ret=0
elif [[ ${service} == tail ]]; then
  _arguments -s -S \
    ${args[@]} \
    '(-f --follow -F)'{-f-,--follow=-}'[output appended data as the file grows]::name or descriptor' \
    '(-f --follow --retry)-F[same as --follow=name --retry]:name:_files' \
    '--max-unchanged-stats=[with --follow=name, reopen a FILE which has not changed size after N]:N' \
    '--pid=[with -f, terminate after process ID, PID dies]:PID:_pids' \
    '--retry[keep trying to open a file even when it is or becomes inaccessible]' \
    '(-c --bytes -n --lines)'{-c+,--bytes=}'[print the last K bytes of each file]: :->k_balue' \
    '(-c --bytes -n --lines)'{-n+,--lines=}'[print the last K lines instead of the first 10]: :->k_value' \
    && ret=0
fi

case ${state} in
  k_value)
    if [[ ${service} == tail ]]; then
      _message -e numbers 'K: normal, +K: exclusive, suffix: b 512, kB 1000, K 1024 MB 1000*1000 M 1024*1024...G, T, P, E, Z, Y' && ret=0
    else
      _message -e numbers 'K: normal, -K: exclusive, suffix: b 512, kB 1000, K 1024 MB 1000*1000 M 1024*1024...G, T, P, E, Z, Y' && ret=0
    fi
    ;;
esac

return ret
