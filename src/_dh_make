#compdef dh_make
#
# --------------------------------------------------------------------------
#
# Description :
#   @(#) zsh completion script for dh_make
#
# --------------------------------------------------------------------------
# Copyright (c) 2015 camisoul<camisoulmax@gmail.com>
#
# This software is provided 'as-is', without any express or implied
# warranty.  In no event will the authors be held liable for any damages
# arising from the use of this software.
#
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it
# freely, subject to the following restrictions:
#
# 1. The origin of this software must not be misrepresented; you must not
#    claim that you wrote the original software. If you use this software
#    in a product, an acknowledgment in the product documentation would be
#    appreciated but is not required.
# 2. Altered source versions must be plainly marked as such, and must not be
#    misrepresented as being the original software.
# 3. This notice may not be removed or altered from any source distribution.
# --------------------------------------------------------------------------

local ret=1
_arguments -s -S \
  '(-c --copyright)'{-c,--copyright}'[use <type> of license in copyright file]:type:(apache artistic bsd gpl gpl2 gpl3 lgpl lgpl2 lgpl3 mit)' \
  '(-C --packageclass)'{-C,--packageclass}'[set package class]:class:(s i k l m n)' \
  '(-e --email)'{-e,--email}'[use <address> as the maintainer e-mail address]:address' \
  '(-n --native)'{-n,--native}"[the program is Debian native, don't generate .orig]" \
  '(-f --file)'{-f,--file}'[specify file to use as the original source archive]:file:_file' \
  '--createorig[create orig.tar.xz file]' \
  '(-s --single)'{-s,--single}'[set package class to single]' \
  '(-i --indep)'{-i,--indep}'[set package class to arch-independent]' \
  '(-m --multi)'{-m,--multi}'[set package class to multiple binary]' \
  '(-l --library)'{-l,--library}'[set package class to library]' \
  '(-k --kmod)'{-k,--kmod}'[set package class to kernel module]' \
  '--kpatch[set package class to kernel patch]' \
  '--with-emacs[add files for emacsen]' \
  '(-a --addmissing)'{-a,--addmissing}'[reprocess package and add missing files]' \
  '(-t --templates)'{-t,--templates}'[apply customizing templates in <dir>]:dir:_files -/' \
  '(-d --defaultless)'{-d,--defaultless}'[skip the default debian and package class templates]' \
  '(-o --overlay)'{-o,--overlay}'[reprocess package using template in <dir>]:dir:_files -/' \
  '(-p --packagename)'{-p,--packagename}'[force package name to be <name>]:name' \
  '(-r --rulesformat)'{-r,--rulesformat}'[Set the format of debian/rules file]:fmt:(old dh7 cdbs)' \
  '(-y --yes)'{-y,--yes}'[Automatic yes to prompts and run non-interactively]' \
  '(- : *)'{-h,--help}'[display this help screen and exit]' \
  '(- : *)'{-v,--version}'[show the version and exit]' && ret=0

return ret
