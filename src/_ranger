#compdef ranger
#
# --------------------------------------------------------------------------
#
# Description :
#   @(#) zsh completion script for ranger
#
# --------------------------------------------------------------------------
# Copyright (c) 2015 camisoul<camisoulmax@gmail.com>
#
# This software is provided 'as-is', without any express or implied
# warranty.  In no event will the authors be held liable for any damages
# arising from the use of this software.
#
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it
# freely, subject to the following restrictions:
#
# 1. The origin of this software must not be misrepresented; you must not
#    claim that you wrote the original software. If you use this software
#    in a product, an acknowledgment in the product documentation would be
#    appreciated but is not required.
# 2. Altered source versions must be plainly marked as such, and must not be
#    misrepresented as being the original software.
# 3. This notice may not be removed or altered from any source distribution.
# --------------------------------------------------------------------------

local ret=1
_arguments -s -S \
  '(- : *)'{-h,--help}'[show help message and exit]' \
  "(- : *)--version[show program's version number and exit]" \
  '(-d --debug)'{-d,--debug}'[activate debug mode]' \
  '(-c --clean)'{-c,--clean}'[do not touch/require any config files]' \
  '(-r --confdir)'{-r,--confdir=}'[change the configuration directory (~/.config/ranger)]:dir:_directories' \
  '--copy-config=[copy the default configs to the local config directory]:which:(all rc rifle commands scope)' \
  '(--choosefile --choosefiles --choosedir)--choosefile=[Makes ranger act like a file chooser]:TARGET:_files' \
  '(--choosefile --choosefiles --choosedir)--choosefiles=[Makes ranger act like a file chooser for multiple files at once]:TARGET:_files' \
  '(--choosefile --choosefiles --choosedir)--choosedir=[Makes ranger act like a directory chooser]:TARGET:_files' \
  '--selectfile=[Open ranger with supplied file selected]:filepath:_files' \
  '--list-unused-keys[List common keys which are not bound to any action]:' \
  '--list-tagged-files=-[List all files which are tagged with the given tag]:tag' \
  '--profile[Print statistics of CPU usage on exit]:' \
  '(-)--cmd=[Execute COMMAND after the configuration has been read]:COMMAND:_command_names -e' && ret=0

return ret
