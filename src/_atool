#compdef atool apack aunpack als acat adiff arepack 
#
# --------------------------------------------------------------------------
#
# Description :
#   @(#) zsh completion script for atool apack aunpack als acat adiff arepack 
#
# --------------------------------------------------------------------------
# Copyright (c) 2015 camisoul<camisoulmax@gmail.com>
#
# This software is provided 'as-is', without any express or implied
# warranty.  In no event will the authors be held liable for any damages
# arising from the use of this software.
#
# Permission is granted to anyone to use this software for any purpose,
# including commercial applications, and to alter it and redistribute it
# freely, subject to the following restrictions:
#
# 1. The origin of this software must not be misrepresented; you must not
#    claim that you wrote the original software. If you use this software
#    in a product, an acknowledgment in the product documentation would be
#    appreciated but is not required.
# 2. Altered source versions must be plainly marked as such, and must not be
#    misrepresented as being the original software.
# 3. This notice may not be removed or altered from any source distribution.
# --------------------------------------------------------------------------

# TODO:2015-08-22:コマンドごとに分岐したい

local ret=1
local -a cmds_args
local -a archive_types
local cmds='-a --add -c --cat -d --diff -x --extract -l --list -X --extract-to -r --repack'
archive_types=(
  {.,.tar,.t}{gz,bz,bz2,Z,lz,xz,7z}
  .tar.lzo .tzo .lzo
  .tar .zip .jar .war
  .rar .lha .lzh
  .alz .ace .arj .arc
  .rpm .deb .cab .cpio
  .lzma .rz .lrz .a
  )

if [[ $service == atool ]]; then
  cmds_args=(
  "($cmds)"{-l,--list}'[List files in archive.]'
  "($cmds)"{-x,--extract}'[Extract files from archive.]'
  "($cmds)"{-X,--extract-to=}'[Extract files from archive to specified directory.]:directory:_directories'
  "($cmds)"{-a,--add}'[Create archive.]'
  "($cmds)"{-c,--cat}'[Extract a file from archive to standard out.]'
  "($cmds)"{-d,--diff}'[Extract two archives and use diff to generate differencies between them.]'
  "($cmds)"{-r,--repack}'[Repacks archives to a differnt format.]'
  )
fi

_arguments -s -S \
  ${cmds_args[@]} \
  '(- 1 *)--version[Output version information and exit.]' \
  '(- 1 *)--help[Show summary of options.]' \
  '(-e --each)'{-e,--each}'[For each argument, execute the specified command.]:files:_files -g "*.('${(j:|:)${archive_types#.}}')"' \
  '(-F --format)'{-F+,--format=}'[Specify arhive format manually.]:archive types:->types' \
  '(-S --simulate)'{-s,--simulate}'[Run atool in simulation mode.]' \
  '(-E --explain)'{-E,--explain}'[Display commands executed by atool.]' \
  '(-p --page)'{-p,--page}'[Run output through a pager.]' \
  '(-f --force)'{-f,--force}'[Allow overwriting of local files.]' \
  '(-D --subdir)'{-D,--subdir}'[Always create a new directory for the archive.]' \
  '(-0 --null)'{-0,--null}'[If no file arguments are specified, the list of files will be read from standard in.]' \
  '*'{-q,--quiet}'[Decrease verbosity level by one.]' \
  '*'{-v,--verbose}'[Increase verbosity level by one.]' \
  '(-q --quiet -v --verbose)'{-V+,--verbosity=}'[Specify verbosity level.]:LEVEL' \
  '(--config)--config=[Load configuration from the specified file.]:FILE:_files' \
  '(-o --option)*'{-o,--option=}'[Override a configuration option.]:KEY=VALUE:->option' \
  '(-O --format-option)*'{-O,--format-option=}'[Send additional options to the archiver command.]:OPTION' \
  '(--save-outdir)--save-outdir=:DIR:_directories' \
  '1:ARCHIVE:_files -g "*.('${(j:|:)${archive_types#.}}')"' \
  '*:FILE:_files' \
  && ret=0

case $state in
  types)
    _describe -t 'types' 'type' archive_types && ret=0
    ;;
  option)
    compset -P '*='
    compset -S '=*'
    _message -e value 'KEY=VALUE' && ret=0
    ;;
esac

return ret
